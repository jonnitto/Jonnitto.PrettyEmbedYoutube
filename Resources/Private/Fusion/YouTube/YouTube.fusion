prototype(YT:YouTube) < prototype(Content) {
	templatePath = 'resource://Jonnitto.PrettyEmbedYoutube/Private/Fusion/YouTube/YouTube.html'
	configuration = ${Configuration.setting('Jonnitto.PrettyEmbedYoutube')}

	@context {
		configuration = ${this.configuration}
		type = ${q(node).property('type') ? q(node).property('type') : 'video'}
		maximumWidth = ${this.maximumWidth ? this.maximumWidth : this.configuration.maximumWidth}
		videoID = YT:VideoID
	}

	isPlaylistANDnoImage = ${type == 'playlist' && !q(node).property('customPreviewImage')}
	wrapper = ${this.configuration.wrapper ? this.configuration.wrapper : false}
	tagName = ${node.context.live ? 'a' : 'div'}

	attributes {
		class = Fusion:RawArray {
			@process.nodeType >
			nodeType = 'jonnitto-prettyembedyoutube-youtube'
			lightbox = ${q(node).property('lightbox') ? configuration.lightbox.class : configuration.embed.class}
		}
		data-fs = ${q(node).property('allowFullScreen') ? 'true' : 'false'}
		data-embed = Fusion:Array {
			src            = ${configuration.embed[type]}
			videoID        = ${videoID}
			var            = ${type == 'video' ? '?' : '&'}
			autoplay       = 'autoplay=1'
			showinfo       = ${q(node).property('showInfo') ? '' : '&showinfo=0'}
			showControls   = ${q(node).property('showControls') ? '' : '&controls=0'}
			loop           = ${q(node).property('loop') ? '&loop=1' : ''}
			closedCaptions = ${q(node).property('closedCaptions') ? '&cc_load_policy=1' : ''}
			showRelated    = ${q(node).property('showRelated') ? '&rel=1' : '&rel=0'}
		}
		href = ${configuration.lightbox[type] + videoID}
		target = '_blank'
	}

	content = Fusion:Tag {
		tagName = 'img'
		attributes {
			class = ${q(node).property('customPreviewImage') ? 'embed-youtube-costum-preview' : 'embed-youtube-youtube-preview'}
			src = YT:ImageSrc
			data-src-retina = YT:ImageSrcRetina
		}
	}
}
